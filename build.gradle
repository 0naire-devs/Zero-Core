plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
}

group = 'kr.hyfata.zero'
version = '1.2.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.6-R0.1-SNAPSHOT")
    compileOnly "com.github.MilkBowl:VaultAPI:1.7.1"
    compileOnly files('libs/ZeroDBCore-Paper.jar')
    implementation 'fr.mrmicky:fastboard:2.1.3'
    implementation 'net.objecthunter:exp4j:0.4.8'
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation("io.github.projectunified:uni-dialog-paper:1.3.1")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

shadowJar {
    // Replace 'com.yourpackage' with the package of your plugin
    relocate 'fr.mrmicky.fastboard', 'kr.hyfata.zero.modules.scoreboard.fastboard'
    relocate 'net.objecthunter.exp4j', 'kr.hyfata.zero.libs.exp4j'
}
kotlin {
    jvmToolchain(21)
}
